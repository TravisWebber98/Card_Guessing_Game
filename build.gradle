plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'  // JLink plugin for packaging with jpackage
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

group = 'com.OOD.Group1'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'com.ood.groupOne.card_guessing_game.MainApp'
}

javafx {
    version = '21'
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.media']
}

// jlink configuration
jlink {
    imageName = 'CardGuessingGame'
    addExtraDependencies("ALL")

    launcher {
        name = 'CardGuessingGame'
    }

    forceMerge('javafx')

    jpackage {
        // Specify the type of package you want (e.g., 'exe' for Windows)
        installerType = 'exe'

        installerName = 'CardGuessingGameInstaller'
        appVersion = '1.0.0'

        // Path to your icon
        icon = file("src/main/resources/icon.ico").absolutePath

        skipInstaller = false  // Set to true if you want to skip installer creation

        vendor = 'Group 1'
        description = 'A JavaFX card guessing game built by Group 1 for OOD'

        // Specify the destination directory for the installer
        outputDir = layout.buildDirectory.dir("jpackage").get().asFile
    }
}

dependencies {
    implementation "org.openjfx:javafx-controls:21"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
}

